plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'antlr'
    id 'maven-publish'
    id 'application'
}

def vertxVersion = '3.0.0'
version = '1.0.0'

sourceCompatibility = 1.8 
targetCompatibility = 1.8

if (!JavaVersion.current().java8Compatible) {
	throw new IllegalStateException(
            '''A Haiku:
            |   This needs Java 8,
            |   You are using something else,
            |   Refresh. Try again.
            '''.stripMargin())
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'name.bpdp.vastix'
            artifactId 'jifa'
            version '1.0.0'
            from components.java
        }
    }
}


repositories {
    mavenCentral()
    maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
    jcenter()
    mavenLocal()
}

dependencies {
    compile 'io.vertx:vertx-core:' + vertxVersion
    antlr "org.antlr:antlr4:4.5.1"
    testCompile 'junit:junit:4.12'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'name.bpdp.vastix.jifa.Jifa'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

mainClassName = 'name.bpdp.vastix.jifa.Jifa'

task copyGenSource(type: Copy) {
    from 'build/generated-src/antlr/main'
    into 'src/main/java/name/bpdp/vastix/jifa/generated'
}

generateGrammarSource {
    maxHeapSize = '64m'
    arguments += ['-visitor', '-long-messages']
    outputDirectory = file('src/main/java/name/bpdp/vastix/jifa/generated')
}
